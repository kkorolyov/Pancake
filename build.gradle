plugins {
	id 'java-library'
	id 'groovy'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.7.3'
}

description = "Extensible Java game engine with an entity-component-system architecture"

subprojects {
	group = 'dev.kkorolyov'
	version = '1.0'

	repositories {
		mavenLocal()  // TODO For early dev only
		jcenter()  // Most things
		maven {
			url 'https://dl.bintray.com/kkorolyov/java'  // SimpleTools
		}
	}
}

configure([	// "Doc"able
    project(':pancake-platform'),
		project('pancake-core'),
		project('skillet'),
		project('killstreek')
]) {
	javadoc {
		destinationDir = file("$rootProject.projectDir/docs/$project.name")
	}
}

configure([	// Testable
		project(':pancake-platform'),
		project(':pancake-core'),
		project(':skillet'),
		project(':killstreek')
]) {
	apply plugin: 'groovy'

	dependencies {
		testImplementation(
				'org.spockframework:spock-core:1.1-groovy-2.+',
				'net.bytebuddy:byte-buddy:1.+',
				'org.objenesis:objenesis:2.+'
		)
	}
}
configure([	// Platform test utilities-dependent
		project(':pancake-core'),
		project(':skillet'),
		project(':killstreek')
]) {
	dependencies {
		testImplementation project(path: ':pancake-platform', configuration: 'platformTest')
	}
}

configure([	// Libraries
		project(':pancake-platform'),
		project(':pancake-core')
]) {
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'

	dependencies {
		api (
				'dev.kkorolyov:simple-props:4.+'
		)
		implementation(
				'dev.kkorolyov:simple-logs:3.+'
		)
	}

	// Artifacts
	task sourceJar(type: Jar) {
		classifier = 'source'
		from sourceSets.main.allJava
	}
	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	// Maven publish
	publishing {
		publications {
			pancake(MavenPublication) {
				from components.java

				artifact sourceJar
				artifact javadocJar
			}
		}
	}
	// Bintray upload
	bintray {
		user = System.getenv('BINTRAY_USER')
		key = System.getenv('BINTRAY_KEY')
		publications = ['pancake']
		pkg {
			repo = 'java'
			name = project.name
			licenses = ['BSD 3-Clause']
			vcsUrl = 'https://github.com/kkorolyov/Pancake.git'
			version {
				name = project.version
			}
		}
	}
}

// Applications
configure([
    project(':skillet'),
		project(':killstreek')
]) {
	apply plugin: 'java'
	apply plugin: 'application'	// Run

	dependencies {
		implementation(
				project(':pancake-platform')
		)
	}
}

project(':pancake-platform') {
	description = "Main Pancake engine platform"


	task testJar(type: Jar) {
		classifier = 'test'
		from sourceSets.test.output
	}

	configurations {
		platformTest
	}
	artifacts {
		platformTest testJar
	}
}

project(':pancake-core') {
	description = "Collection of general, reusable systems and components for the Pancake engine"

	dependencies {
		implementation project(':pancake-platform')
	}
}

project(':skillet') {
	description = "Standalone application for designing and exporting entities"

	mainClassName = 'dev.kkorolyov.pancake.skillet.Skillet'
}

project(':killstreek') {
	description = "Top-down ARPG with dynamic RNG system"

	dependencies {
		implementation(
				project(':pancake-core')
		)
	}
	mainClassName = 'dev.kkorolyov.killstreek.Launcher'
}

// Its own thing
project(':test-interactive') {
	apply plugin: 'application'

	description = "Interactive test of core modules"

	dependencies {
		implementation(
				project(':pancake-platform'),
				project(':pancake-core')
		)
	}
	mainClassName = 'dev.kkorolyov.pancake.test.Interactive'
}
