plugins {
	id 'com.gradle.build-scan' version '1.9'
	id 'java-library'
	id 'groovy'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.7.3'
}

description = "Extensible Java game engine with an entity-component-system architecture"

buildScan {
	licenseAgreementUrl = 'https://gradle.com/terms-of-service'
	licenseAgree = 'yes'
}

subprojects {
	group = 'dev.kkorolyov'
	version = '1.0'

	repositories {
		mavenLocal()  // TODO For early dev only
		jcenter()  // Most things
		maven {
			url 'https://dl.bintray.com/kkorolyov/java'  // SimpleTools
		}
	}
}

// Projects
def platform = project(':pancake-platform')
def core = project(':pancake-core')
def skillet = project(':skillet')
def killstreek = project(':killstreek')
def testInteractive = project(':test-interactive')

configure([	// "Doc"able
    platform,
		core,
		skillet,
		killstreek
]) {project ->
	apply plugin: 'java'

	javadoc {
		source = sourceSets.main.allJava
		destinationDir = file("$rootProject.projectDir/docs/$project.name")
	}
}

configure([	// Testable
		platform,
		core,
		skillet,
		killstreek
]) {
	apply plugin: 'groovy'

	dependencies {
		testImplementation(
				'org.spockframework:spock-core:1.1-groovy-2.+',
				'net.bytebuddy:byte-buddy:1.+',
				'org.objenesis:objenesis:2.+'
		)
	}
}
configure([	// Platform test utilities-dependent
		core,
		skillet,
		killstreek
]) {
	dependencies {
		testImplementation project(path: ':pancake-platform', configuration: 'platformTest')
	}
}

configure([	// Libraries
		platform,
		core
]) {project ->
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'com.jfrog.bintray'

	// Artifacts
	task sourceJar(type: Jar) {
		classifier = 'source'
		from sourceSets.main.allJava
	}
	task javadocJar(type: Jar) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	// Maven publish
	publishing {
		publications {
			pancake(MavenPublication) {
				from components.java

				artifact sourceJar
				artifact javadocJar
			}
		}
	}
	// Bintray upload
	afterEvaluate {
		bintray {
			user = System.getenv('BINTRAY_USER')
			key = System.getenv('BINTRAY_KEY')
			publications = ['pancake']
			pkg {
				repo = 'java'
				name = project.name
				licenses = ['BSD 3-Clause']
				vcsUrl = 'https://github.com/kkorolyov/Pancake.git'
				version {
					name = project.version
				}
			}
		}
	}

	task releaseLocal(dependsOn: [javadoc, test, publishToMavenLocal]) {
		group 'publishing'
		description "Prepares all release resources and publishes to the local Maven repo"

		doLast {
			println "Released ${project.name}-${project.version} locally"
		}
	}
	task releaseBintray(dependsOn: [releaseLocal, bintrayUpload]) {
		group 'publishing'
		description "Releases to Bintray"

		doLast {
			println "Released ${project.name}-${project.version} to Bintray"
		}
	}
}

configure([	// Applications
    skillet,
		killstreek,
		testInteractive
]) {
	apply plugin: 'java'
	apply plugin: 'application'	// Run
}

configure([	// Platform-reliant
    core,
		skillet,
		killstreek,
		testInteractive
]) {
	dependencies {
		implementation platform
	}
}
configure([	// Core-reliant
    killstreek,
		testInteractive
]) {
	dependencies {
		implementation core
	}
}

configure([	// SimpleFiles-reliant
		platform,
		skillet,
		testInteractive
]) {
	dependencies {
		implementation 'dev.kkorolyov:simple-files:1.+'
	}
}

project(':pancake-platform') {
	description = "Main Pancake engine platform"

	dependencies {
		api(
				'dev.kkorolyov:simple-logs:3.+',
				'dev.kkorolyov:simple-props:4.+'
		)
	}

	task testJar(type: Jar) {
		classifier = 'test'
		from sourceSets.test.output
	}

	configurations {
		platformTest
	}
	artifacts {
		platformTest testJar
	}
}

project(':pancake-core') {
	description = "Collection of general, reusable systems and components for the Pancake engine"
}

project(':skillet') {
	description = "Standalone application for designing and exporting entities"

	mainClassName = 'dev.kkorolyov.pancake.skillet.Skillet'
}

project(':killstreek') {
	description = "Top-down ARPG with dynamic RNG system"

	mainClassName = 'dev.kkorolyov.killstreek.Launcher'
}

project(':test-interactive') {
	description = "Interactive test of core modules"

	mainClassName = 'dev.kkorolyov.pancake.test.Interactive'
}
